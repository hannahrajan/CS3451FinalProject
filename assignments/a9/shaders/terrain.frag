#version 330 core

layout (std140) uniform camera
{
	mat4 projection;
	mat4 view;
	mat4 pvm;
	mat4 ortho;
	vec4 position;
};

/* set light ubo. do not modify.*/
struct light
{
	ivec4 att; 
	vec4 pos; // position
	vec4 dir;
	vec4 amb; // ambient intensity
	vec4 dif; // diffuse intensity
	vec4 spec; // specular intensity
	vec4 atten;
	vec4 r;
};
layout(std140) uniform lights
{
	vec4 amb;
	ivec4 lt_att; // lt_att[0] = number of lights
	light lt[4];
};

uniform float iTime;
uniform mat4 model;		/*model matrix*/

in vec3 vtx_pos;

out vec4 frag_color;


uniform vec3 ka;            /* object material ambient */
uniform vec3 kd;            /* object material diffuse */
uniform vec3 ks;            /* object material specular */
uniform float shininess;    /* object material shininess */

vec2 hash2(vec2 v)
{
	vec2 rand = vec2(0,0);
	
	rand  = 50.0 * 1.05 * fract(v * 0.3183099 + vec2(0.71, 0.113));
    rand = -1.0 + 2 * 1.05 * fract(rand.x * rand.y * (rand.x + rand.y) * rand);
	return rand;
}

float perlin_noise(vec2 v) 
{
    float noise = 0;
	vec2 i = floor(v);
    vec2 f = fract(v);
    vec2 m = f*f*(3.0-2.0*f);
	
	noise = mix( mix( dot( hash2(i + vec2(0.0, 0.0)), f - vec2(0.0,0.0)),
					 dot( hash2(i + vec2(1.0, 0.0)), f - vec2(1.0,0.0)), m.x),
				mix( dot( hash2(i + vec2(0.0, 1.0)), f - vec2(0.0,1.0)),
					 dot( hash2(i + vec2(1.0, 1.0)), f - vec2(1.0,1.0)), m.x), m.y);
	return noise;
}

float noiseOctave(vec2 v, int num)
{
	float sum = 0;
	for(int i =0; i<num; i++){
		sum += pow(2,-1*i) * perlin_noise(pow(2,i) * v);
	}
	return sum;
}

float height(vec2 v){
    float h = 0;
	h = 0.3 * noiseOctave(v, 10);
	if(h<0) h = 0;
	return h * 2.;
}

vec3 compute_normal(vec2 v, float d)
{	
	vec3 normal_vector = vec3(0,0,0);
	vec3 v1 = vec3(v.x + d, v.y, height(vec2(v.x + d, v.y)));
	vec3 v2 = vec3(v.x - d, v.y, height(vec2(v.x - d, v.y)));
	vec3 v3 = vec3(v.x, v.y + d, height(vec2(v.x, v.y + d)));
	vec3 v4 = vec3(v.x, v.y - d, height(vec2(v.x, v.y - d)));
	
	normal_vector = normalize(cross(v1-v2, v3-v4));
	return normal_vector;
}

vec4 shading_phong(light li, vec3 e, vec3 p, vec3 s, vec3 n) 
{
    vec3 v = normalize(e - p);
    vec3 l = normalize(s - p);
    vec3 r = normalize(reflect(-l, n));

    vec3 ambColor = ka * li.amb.rgb;
    vec3 difColor = kd * li.dif.rgb * max(0., dot(n, l));
    vec3 specColor = ks * li.spec.rgb * pow(max(dot(v, r), 0.), shininess);

    return vec4(ambColor + difColor + specColor, 1);
}

// Draw the terrain
vec3 shading_terrain(vec3 pos) {
	vec3 n = compute_normal(pos.xy, 0.01);
	vec3 e = position.xyz;
	vec3 p = pos.xyz;
	vec3 s = lt[0].pos.xyz;

    n = normalize((model * vec4(n, 0)).xyz);
    p = (model * vec4(p, 1)).xyz;
	
    vec3 color = vec3(0.0);
    for (int i = 0; i < lt_att[0]; i++) {
        vec3 s = lt[i].pos.xyz;
        color += shading_phong(lt[i], e, p, s, n).xyz;
    }
	
	float h = pos.z + .8;
	h = clamp(h, 0.0, 1.0);
	float step_height = 0.1;

	//// MOUNDS + SEAWEED - random, ~90% mounds 10% seaweed
	vec3 top_mound_color = vec3(227 / 255.,200 / 255.,125 / 255.);
	vec3 bottom_mound_color = vec3(232 / 255.,198 / 255.,153 / 255.);

	vec3 top_seaweed_color = vec3(19 / 255.f,189 / 255.f,75 / 255.f);
	vec3 bottom_seaweed_color = vec3(4 / 255.f,133 / 255.f,47 / 255.f);

	float r = fract(sin(dot(floor(pos.xy),vec2(127.1,311.7))) * 43758.5453123); //new hash - should give even 50/50 split

	float band = floor((pos.z - 0.0001) / step_height); 
	float local_h = fract((pos.z - 0.0001) / step_height);

	bool even_band = mod(int(band), 2) == 0;

	vec3 colorA = vec3(0.0);
	vec3 colorB = vec3(0.0);

	if (r < 0.9){
		colorA = even_band ? top_mound_color : bottom_mound_color;
		colorB = even_band ? bottom_mound_color : top_mound_color;
	} else {
		colorA = even_band ? top_seaweed_color : bottom_seaweed_color;
		colorB = even_band ? bottom_seaweed_color : top_seaweed_color;
	}

	vec3 emissive_color = mix(colorA, colorB, smoothstep(0.0, 1.0, local_h));

	return color * emissive_color;
}

void main()
{
	float radius = 2.5;
    vec2 center = vec2(2.5);
    float dist = length(vtx_pos.xy - center);
    
    // Create circular mask with 1.0 inside, 0.0 outside
    float circle_mask = step(dist, radius);
	//frag_color = vec4(shading_terrain(vtx_pos), 1.0);
    frag_color = mix(vec4(0.0, 0.0, 0.0, 0.0), vec4(shading_terrain(vtx_pos), 1.0), circle_mask);
    
}
